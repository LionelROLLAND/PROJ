{
    "cells": [
        {
            "cell_type": "markdown",
            "metadata": {},
            "source": [
                "## Sandbox\n",
                "\n",
                "Un  `.ipynb` pour tester des idÃ©es :)"
            ]
        },
        {
            "cell_type": "code",
            "execution_count": null,
            "metadata": {},
            "outputs": [],
            "source": [
                "using Pkg\n",
                "Pkg.activate(\".\")\n",
                "Pkg.instantiate()\n",
                "using Graphs\n",
                "using MetaGraphsNext\n",
                "using JuMP\n",
                "using Gurobi\n",
                "\n",
                "ENV[\"GRB_LICENSE_FILE\"] = \"gurobi.lic\"\n",
                "include(joinpath(\"src\", \"utils.jl\"));"
            ]
        },
        {
            "cell_type": "code",
            "execution_count": null,
            "metadata": {},
            "outputs": [],
            "source": [
                "infos = open(\n",
                "    readInstance,\n",
                "    file,\n",
                ");"
            ]
        },
        {
            "cell_type": "code",
            "execution_count": null,
            "metadata": {},
            "outputs": [],
            "source": [
                "graph = graphFromData(infos);"
            ]
        },
        {
            "cell_type": "code",
            "execution_count": null,
            "metadata": {},
            "outputs": [],
            "source": [
                "graph[]"
            ]
        },
        {
            "cell_type": "code",
            "execution_count": null,
            "metadata": {},
            "outputs": [],
            "source": [
                "EdgeData = @NamedTuple begin\n",
                "    is_s::Bool\n",
                "    is_t::Bool\n",
                "    p::Int64\n",
                "    ph::Int64\n",
                "end\n",
                "\n",
                "mutable struct HeurVertex\n",
                "    is_s::Bool\n",
                "    is_t::Bool\n",
                "    p::Int64\n",
                "    ph::Int64\n",
                "    weight::Float64\n",
                "end\n",
                "\n",
                "mutable struct HeurEdge\n",
                "    d::Float64\n",
                "    big_d::Float64\n",
                "    weight::Float64\n",
                "    src_node::Ref{HeurVertex}\n",
                "    node_multiplier::Ref{Float64}\n",
                "end\n",
                "\n",
                "HeurParams = @NamedTuple begin\n",
                "    m_start::Float64\n",
                "    m_smoother::Function\n",
                "    v_smoother::Function\n",
                "    e_smoother::Function\n",
                "end\n",
                "\n",
                "function baseSmoother(n::Real)::Real\n",
                "    return n / (n + 1)\n",
                "end\n",
                "\n",
                "default_heur_params = (\n",
                "    m_start=1.0,\n",
                "    m_smoother=baseSmoother,\n",
                "    v_smoother=n -> 1 + n / 10 |> baseSmoother,\n",
                "    e_smoother=n -> 1 + n / 10 |> baseSmoother,\n",
                ")\n",
                "\n",
                "function heurWeight(e::HeurEdge)::Float64\n",
                "    return e.weight + e.node_multiplier[] * e.src_node[].weight\n",
                "end\n",
                "\n",
                "# /!\\ Prends les s, t du format Graphs.jl, renvoie un path format Graphs.jl (et pas MetaGraphsNext)\n",
                "function mkPath(state::Graphs.DijkstraState{Float64, Int64}; s::Int64, t::Int64)::Vector{Int64}\n",
                "    res::Vector = [t]\n",
                "    while t != s\n",
                "        t = state.parents[t]\n",
                "        pushfirst!(res, t)\n",
                "    end\n",
                "    return res\n",
                "end\n",
                "\n",
                "function heuristicSolve(\n",
                "    graph;\n",
                "    params=default_heur_params,\n",
                "    timelimit::Float64=time()+10.0,\n",
                ")::Tuple{Vector{Int64}, Float64}\n",
                "\n",
                "    node_multiplier::Ref{Float64} = Ref{Float64}(params.m_start)\n",
                "\n",
                "    heur_graph = MetaGraph(\n",
                "        DiGraph();\n",
                "        label_type=Int64,\n",
                "        vertex_data_type=HeurVertex,\n",
                "        edge_data_type=HeurEdge,\n",
                "        weight_function=heurWeight,\n",
                "        default_weight=Inf,\n",
                "        graph_data=graph[],\n",
                "    )\n",
                "\n",
                "    function createNode(node::Int64)::Nothing\n",
                "        heur_graph[node] = HeurVertex(\n",
                "            graph[node].is_s,  # is_s\n",
                "            graph[node].is_t,  # is_t\n",
                "            graph[node].p,  # p\n",
                "            graph[node].ph,  # ph\n",
                "            graph[node].p,  # weight\n",
                "        )\n",
                "        return nothing\n",
                "    end\n",
                "\n",
                "    function createEdge((n1, n2)::Tuple{Int64, Int64})::Nothing\n",
                "        heur_graph[n1, n2] = HeurEdge(\n",
                "            graph[n1, n2].d,  # d\n",
                "            graph[n1, n2].big_d,  # big_d\n",
                "            graph[n1, n2].d,  # weight\n",
                "            Ref{HeurVertex}(heur_graph[n1]),  # src_node\n",
                "            node_multiplier,\n",
                "        )\n",
                "        return nothing\n",
                "    end\n",
                "\n",
                "    # Prends un path au format MetaGraphsNext.jl\n",
                "    function trueNodeWeights(path::Vector{Int64})::Dict{Int64, Float64}\n",
                "        sorted_nodes::Vector{Int64} = sort(path, by=v -> heur_graph[v].ph, rev=true)\n",
                "        res::Dict{Int64, Float64} = Dict{Int64, Float64}()\n",
                "        d2::Float64 = heur_graph[].d2\n",
                "        rem::Float64 = 0\n",
                "        for node in sorted_nodes\n",
                "            rem = min(d2, 2)\n",
                "            res[node] = heur_graph[node].p + rem * heur_graph[node].ph\n",
                "            d2 -= rem\n",
                "        end\n",
                "        return res\n",
                "    end\n",
                "\n",
                "    # Prends un path au format MetaGraphsNext.jl\n",
                "    function trueEdgeWeights(path::Vector{Int64})::Dict{Tuple{Int64, Int64}, Float64}\n",
                "        arcs::Vector{Tuple{Int64, Int64}} = collect((path[i], path[i+1]) for i in 1:length(path) - 1)\n",
                "        sort!(arcs, by=((v1, v2),) -> heur_graph[v1, v2].d, rev=true)\n",
                "        res::Dict{Tuple{Int64, Int64}, Float64} = Dict{Tuple{Int64, Int64}, Float64}()\n",
                "        d1::Float64 = heur_graph[].d1\n",
                "        rem::Float64 = 0\n",
                "        for (v1, v2) in arcs\n",
                "            rem = min(d1, heur_graph[v1, v2].big_d)\n",
                "            res[(v1, v2)] = heur_graph[v1, v2].d * (1 + rem)\n",
                "            d1 -= rem\n",
                "        end\n",
                "        return res\n",
                "    end\n",
                "\n",
                "    function updateNWeights(n_weights::Dict{Int64, Float64})::Nothing\n",
                "        for (node, weight) in pairs(n_weights)\n",
                "            heur_graph[node].weight = (\n",
                "                params.v_smoother(n) * heur_graph[node].weight\n",
                "                +\n",
                "                (1 - params.v_smoother(n)) * weight\n",
                "            )\n",
                "        end\n",
                "        return nothing\n",
                "    end\n",
                "\n",
                "    function updateAWeights(a_weights::Dict{Tuple{Int64, Int64}, Float64})::Nothing\n",
                "        for ((v1, v2), weight) in pairs(a_weights)\n",
                "            heur_graph[v1, v2].weight = (\n",
                "                params.e_smoother(n) * heur_graph[v1, v2].weight\n",
                "                +\n",
                "                (1 - params.e_smoother(n)) * weight\n",
                "            )\n",
                "        end\n",
                "        return nothing\n",
                "    end\n",
                "\n",
                "    foreach(createNode, labels(graph))\n",
                "    foreach(createEdge, edge_labels(graph))\n",
                "\n",
                "    n::Int64 = 1\n",
                "    best_sol_value::Float64 = Inf\n",
                "    best_sol::Vector{Int64} = Vector{Int64}()\n",
                "    while time() <= timelimit - 1\n",
                "        state::Graphs.DijkstraState{Float64, Int64} = dijkstra_shortest_paths(\n",
                "            heur_graph,\n",
                "            code_for(heur_graph, heur_graph[].s),\n",
                "        )\n",
                "        path::Vector{Int64} = map(\n",
                "            node -> label_for(heur_graph, node),  # Passe du format Graphs.jl au format MetaGraphsNext.jl\n",
                "            mkPath(\n",
                "                state;\n",
                "                s=code_for(heur_graph, heur_graph[].s),\n",
                "                t=code_for(heur_graph, heur_graph[].t),\n",
                "            )\n",
                "        )\n",
                "        n_weights::Dict{Int64, Float64} = trueNodeWeights(path)\n",
                "        updateNWeights(n_weights)\n",
                "\n",
                "        a_weights::Dict{Tuple{Int64, Int64}, Float64} = trueEdgeWeights(path)\n",
                "        updateAWeights(a_weights)\n",
                "\n",
                "        if sum(values(n_weights)) <= heur_graph[].big_s\n",
                "\n",
                "            node_multiplier[] *= params.m_smoother(n)\n",
                "\n",
                "            if sum(values(a_weights)) < best_sol_value\n",
                "                best_sol_value = sum(values(a_weights))\n",
                "                best_sol = path\n",
                "            end\n",
                "\n",
                "        else\n",
                "            node_multiplier[] /= params.m_smoother(n)\n",
                "        end\n",
                "        n += 1\n",
                "    end\n",
                "    return best_sol, best_sol_value\n",
                "end"
            ]
        },
        {
            "cell_type": "code",
            "execution_count": null,
            "metadata": {},
            "outputs": [],
            "source": [
                "sol, val = heuristicSolve(graph)"
            ]
        }
    ],
    "metadata": {
        "kernelspec": {
            "display_name": "Julia 1.7.2",
            "language": "julia",
            "name": "julia-1.7"
        },
        "language_info": {
            "file_extension": ".jl",
            "mimetype": "application/julia",
            "name": "julia",
            "version": "1.7.2"
        }
    },
    "nbformat": 4,
    "nbformat_minor": 2
}
